// --------------------------------------
// Viconia Friendship Setup
//
// Version:   V4.3
// Author(s): K'aeloree
// --------------------------------------
// Backup directory
BACKUP ~viconia/backup~

// Error reports sent to K'aeloree
AUTHOR ~kae@spellholdstudios.net~

// Version
VERSION ~v4.3~

// Readme
README ~viconia/viconiafriendship.html~

// Translations
AUTO_TRA ~viconia/translations/%s~


ALWAYS
  ACTION_IF GAME_IS ~eet~ BEGIN
    OUTER_SET bg2_chapter = 12
  END ELSE BEGIN
    OUTER_SET bg2_chapter = 0
  END
  OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
    OUTER_SET bg2_chapter = bg2_chapter + 1
    OUTER_SPRINT name_source ~bg2_chapter_%i%~
    OUTER_SET EVAL ~%name_source%~ = bg2_chapter
  END

  // No exception, everything needs to be converted
  ACTION_DEFINE_ARRAY ViFrienoconvert BEGIN END

  // any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
  ACTION_DEFINE_ARRAY ViFriereload BEGIN Setup END

  LAF HANDLE_CHARSETS
    INT_VAR
      infer_charsets = 1
    STR_VAR
      tra_path = EVAL ~%MOD_FOLDER%/Translations~
      noconvert_array = ViFrienoconvert
      reload_array = ViFriereload
  END
END


LANGUAGE ~English~
         ~english~
         ~viconia/translations/english/setup.tra~
LANGUAGE ~French~
         ~french~
         ~viconia/translations/french/setup.tra~
LANGUAGE ~Russian~
         ~russian~
         ~viconia/translations/russian/setup.tra~
LANGUAGE ~Chinese~
         ~schinese~
         ~viconia/translations/schinese/setup.tra~

// Component
BEGIN @0
  REQUIRE_PREDICATE GAME_IS ~bg2ee tob bgt eet~ @5

// --------------------------------------
// Ids
// --------------------------------------
// Add custom IVFPD state
APPEND ~state.ids~
  ~0x80101FEF CD_STATE_NOTVALID~
  UNLESS ~CD_STATE_NOTVALID~
  
// Ensures banters won't run in BG1 section in Baldur's Gate Trilogy
ACTION_IF FILE_EXISTS ~DATA/BG1ARE.BIF~ THEN BEGIN
    OUTER_SPRINT "percentage_sign" "%"
    OUTER_SPRINT ~BGT_VAR~ ~Global("ENDOFBG1","GLOBAL",2)~
END ELSE BEGIN
    OUTER_SPRINT "percentage_sign" "%"
    OUTER_SPRINT ~BGT_VAR~ ~~
END


// --------------------------------------
// Dialogue
// --------------------------------------
// Compile dialogue
COMPILE ~viconia/dialogue/viconij.d~
  USING ~viconia/translations/english/viconij.tra~
  	~viconia/translations/%s/viconij.tra~

COMPILE ~viconia/dialogue/vicon25j.d~
  USING ~viconia/translations/english/vicon25j.tra~
       	~viconia/translations/%s/vicon25j.tra~


// --------------------------------------
// Scripts
// --------------------------------------
// Extend scripts
EXTEND_BOTTOM ~viconia.bcs~ ~viconia/scripts/viconia.baf~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~vicond.bcs~ ~viconia/scripts/vicond.baf~
  EVALUATE_BUFFER
  
EXTEND_TOP ~vico25.bcs~ ~viconia/scripts/vico25.baf~


// --------------------------------------
// Audio (ahahaha)
// --------------------------------------
COPY ~viconia/audio/i#blank.wav~ ~override~


// --------------------------------------
// Creatures
// --------------------------------------
// Handmaiden of Lolth
COPY_EXISTING ~vicpri01.cre~ ~override/i#vhand.cre~
  SAY                   NAME1           ~Handmaiden of Lolth~
  SAY                   NAME2           ~Handmaiden of Lolth~
  WRITE_EVALUATED_ASCII DIALOG          ~%DEST_RES%~ #8
  WRITE_EVALUATED_ASCII DEATHVAR        ~%DEST_RES%~ #32
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~%DEST_RES%~ #8
         
// Yochlol
  COPY_EXISTING ~yochlo01.cre~ ~override/i#vyoch.cre~
  SAY                   NAME1           ~Yochlol~
  SAY                   NAME2           ~Yochlol~
  WRITE_EVALUATED_ASCII DIALOG          ~%DEST_RES%~ #8
  WRITE_EVALUATED_ASCII DEATHVAR        ~%DEST_RES%~ #32
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~%DEST_RES%~ #8


// --------------------------------------
// New epilogues if ToB is installed
// --------------------------------------
  COPY ~viconia/epilogues/i#vic.2da~ ~override/i#vic1.2da~
     REPLACE ~99999~ @1

  COPY ~viconia/epilogues/i#vic.2da~ ~override/i#vic2.2da~
     REPLACE ~99999~ @2
     
  COPY ~viconia/epilogues/i#vic.2da~ ~override/i#vic3.2da~
     REPLACE ~99999~ @3
     
  COPY ~viconia/epilogues/i#vic.2da~ ~override/i#vic4.2da~
     REPLACE ~99999~ @4

  EXTEND_TOP ~ar6200.bcs~ ~viconia/scripts/ar6200.baf~
